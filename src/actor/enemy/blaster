actor "8C Blaster" : "8C Enemy" {
    obituary "$8C_OBIT_BLASTER"
    +NOGRAVITY radius 22 height 40
    meleerange 1024 accuracy 14
        states { 
            spawn:
                BLAD A 0 nodelay A_GiveInventory("8C Deflect On")
            idle:
                8CAA S 20 A_LookEx(0,0,meleerange,1,80,"see")
                loop
            see:
                8CAA T 4
                8CAA U 4 A_GiveInventory("8C Deflect Off")
                8CAA V 10 A_SetArg(0,3)
            pew:
                BEAK D 0 A_ClearTarget
                BEAK D 0 A_LookEx(0,0,meleerange,1,80,1) goto close
                BEAK D 0 A_PlaySoundEx("8CBLASTR", "Weapon")
                BEAK D 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET)
                8CAA V 20 A_SpawnItemEx("8C Blaster shot",24,0,20,0,0,0,0,SXF_TRANSFERTRANSLATION)
                BEAK D 0 A_SetArg(0,args[0]-1)
                BEAK D 0 A_JumpIf(!args[0], "close")
                loop
            close:
                8CAA U 4
                8CAA T 4 A_GiveInventory("8C Deflect On")
                8CAA S 35 A_ClearTarget
                goto idle
        }
}

actor "8C Blaster V2" : "8C Blaster" {}
actor "8C Blaster V3" : "8C Blaster" {}

actor "8C Blaster shot" : proj {
    damage (7) height 15 radius 7 accuracy 20 states {
        spawn:
            8CAA F 0 nodelay A_TransferPointer(AAPTR_TARGET, AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
            8CAA F 0 ACS_NamedExecuteWithResult("core_predicthome", accuracy, AAPTR_TRACER, 0)
            8CAA F 0 A_ChangeVelocity(cos(pitch)*accuracy, 0, sin(-pitch)*accuracy,CVF_RELATIVE|CVF_REPLACE)
            8CAA F 0 A_SpawnItemEx("8C Puff",0,0,-15,0,0,0,0,SXF_TRANSFERTRANSLATION)
            8CAA F -1
            wait}}

actor "8C Puff" : gfx {
    states { spawn: 8CAA GHI 3 
        stop}}