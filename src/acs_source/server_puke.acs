#DEFINE PUKE_WAITING 0
#DEFINE PUKE_TIMED_OUT 1
#DEFINE PUKE_OK 1<<1
#DEFINE PUKE_ERROR 1<<2

#DEFINE PUKE_TIMEOUT_DURATION 35*4 // tics

#DEFINE PUKE_SILENT 1<<16

script "join" (void) NET {
    ACS_NamedExecuteWithResult("8C Puke", (random(1,20000)<<16)|PUKE_HERO_CREATE, -1, 0);
    delay(25+2);
    ACS_NamedExecuteWithResult("8C Puke", (random(1,20000)<<16)|PUKE_ENTER_GAME, -1);
    delay(2);
    consolecommand("menu_joingame");
}

script "8C Puke" (int type, int arg1, int arg2, int arg3) NET {
    int requestId = type&0xFFFF0000;
    // throttling, or already answered this request 
	if (CheckInventory("8C Puke Request") || (CheckInventory("8C Puke Response")&0xFFFF0000)==requestId) {    
		terminate; 
	}
	GiveInventory("8C Puke Request", 1);
    clearInv("8C Puke Response");
    int player = PlayerNumber();
    pukeReply(requestId);
    str pukeDebug = strParam(
        s:"\cn--> PUKE #", d:requestId>>16, s:" (", d:player, s:")",
        s:" [net ", d:arg1&0xFFFF, 
        s:" - arg1 ", d:arg1, 
        s:" - arg2 ", d:arg2, s:"]"
    );
    log(s:pukeDebug);
	switch(type&0xFFFF) {
        
        /*
        @arg1 slot
        @arg2 hero index
        */
		case PUKE_HERO_CREATE:
            Log(s:"hero create player ", d:player, s:" slot ", d:arg1, s:" hero index ", d:arg2);
            if ((arg2<0) || (arg2>=getHeroSize())) {
				pukeReply(PUKE_ERROR); terminate; 
            }
            if (!slotSanityCheck(player, arg1, true)) {
                pukeReply(PUKE_ERROR); terminate;
            }
            dbEvent(DB_CREATE_HERO,player,arg1,arg2);
            pukeReply(PUKE_OK);
		break;
        
        /*
        @arg1 slot to delete
        */
		case PUKE_HERO_DELETE:
            Log(s:"hero delete player ", d:player, s:" slot ", d:arg1);
			if (!PlayerIsSpectator(player)) {
				pukeReply(PUKE_ERROR); terminate;
            }
            if (!slotSanityCheck(player, arg1, false)) {
                pukeReply(PUKE_ERROR); terminate;
            }
            dbEvent(DB_DELETE_HERO,player,arg1,NULL);
            pukeReply(PUKE_OK);
		break;

        /*
        @arg1 slot to enter game with
        */
        case PUKE_ENTER_GAME:
            Log(s:"player ", d:player, s:" wants to enter with slot ", d:arg1);
            if (!slotSanityCheck(player,arg1,false)) {
				pukeReply(PUKE_ERROR); terminate; 
            }
            setAllowedSlot(player,arg1);
            pukeReply(PUKE_OK);
        break;

        /* no args */
		case PUKE_LOGIN_REFRESH:
            if (!isNetworkGame()) {
				pukeReply(PUKE_ERROR); terminate;
            }
			else if (!PlayerIsLoggedIn(player)) {
				setLogin(player, "");
				pukeReply(PUKE_ERROR); terminate;
			}
            pukeReply(PUKE_OK);
            if (isLoggedIn(player))                 
                terminate;
            else {
                dbEvent(DB_ACCOUNT_LOAD, player, NULL, NULL);
                setLogin(player, GetPlayerAccountName(player));
            }
		break;

        /*
        @arg1 0 for goal leave ; 1 for join
        */
        case PUKE_GOAL:
            if (!CheckInventory("8C Goal Status")) {
                pukeReply(PUKE_ERROR); terminate;
            }
            // leave 
            if (!arg1) { 
                if (!(CheckInventory("8C Goal Status")>>8)) {
                    pukeReply(PUKE_ERROR); terminate;
                }
                pukeReply(PUKE_OK);
                ACS_NamedExecuteWithResult("core_freezeplayer",0,false);
                setGoal(getGoal()-1);
                SetInventory("8C Goal Status", 1);
                delay(15);
                ACS_NamedExecuteWithResult("8C", C8_PACIFIST, false);
                delay(55);
                clearInv("8C Goal Status");
            }
            // join
            else { 
                if (CheckInventory("8C Goal Status")>>8) {
                    pukeReply(PUKE_ERROR); terminate;
                }
                pukeReply(PUKE_OK);
                SetInventory("8C Goal Status", 1<<8);
                setGoal(getGoal()+1);
				ACS_NamedExecuteWithResult("8C", GAME_STATUS_CHECK);
            }
        break;
	}
}

function bool slotSanityCheck(int player, int slot, bool mustBeEmpty) {
    if ((slot < -1) | (slot >= MAX_SLOTS)) {
        log(s:"// slot out of bounds");
        return false;
    } 
    else if (mustBeEmpty!=isSlotEmpty(player,slot)) {
        log(s:"// slot availability not satisfied");
        return false;
    }
    return true;
}

// server -> client
function void pukeReply(int responseCode) { GiveInventory("8C Puke Response", responseCode); }
// client -> client
function int getPukeStatus(void) { return CheckInventory("8C Puke Status"); }
function void setPukeStatus(int status) { SetInventory("8C Puke Status", status); }