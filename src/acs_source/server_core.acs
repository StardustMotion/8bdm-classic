


#DEFINE INCARNATION_PREINTRO_TIME 25 // tics before actor plays its intro8C state
#DEFINE INCARNATION_INTRO_SPAWN_TIME 90 // tics allowed for intro8C duration
#DEFINE INCARNATION_INTRO_TIME INCARNATION_PREINTRO_TIME+INCARNATION_INTRO_SPAWN_TIME
#DEFINE INCARNATION_INTRO_READY_TIME INCARNATION_PREINTRO_TIME+25 // "ready" animation display time

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ Core scripts ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //

script "8C" (int type, int arg1, int arg2, int arg3) {
	switch(type) {

		case PHYSICS_8C:
			GiveInventory("8C Static Move Giver", 1);
			SetAirControl(8.392333); // 5.905 for airspeed = groundspeed
			
			/*
			while (true) { // airFriction control }}
			Player air movement was planned to have lower friction + slower player speed,
				resulting in jumps being hard to readjust direction, but gaining more momentum/speed (like doom jumps.)
			This, as a contract to ground movement - high friction + high player speed, resulting in good dodging/position,
			but not good to cover distances.

			It was dropped because modifying player's speed based on grounded/in air (with setactorproperty or powerspeed i.e)
			couldn't use client-side prediction (https://zdoom.org/wiki/Creating_multiplayer-friendly_ZScript)
			to make player movement online smooth.

			Need Player.Forward/Side move properties but for in-air specific :D

			-----------------------------------------------------------------------------------------------

			Also for some reason when an actor (the player) is standing on a solid actor (i.e yoku block),
			the aircontrol (friction) stops applying, giving player a surge of speed
			We reposition the player 1 unit above the solid actor, with no gravity, just so aircontrol applies again (code below)
				
			Limitations though : there's still 1 tic of speed surge (when landing on the actor),
			also jump reproduction not accurate ; it allows jumping again as soon as you land on the actor
			*/
			while(!isGone(NULL)) {
				if ((GetActorZ(0)-GetActorFloorZ(0))>=1.0)
					if (!checkFooting(true,-1.0)) {
						SetActorVelocity(0,GetActorVelX(0),GetActorVelY(0),0.0,false,false);
						SetActorPosition(0,GetActorX(0),GetActorY(0),GetActorZ(0)+1.0,false);
						GiveInventory("8C On actor gravity Giver", 1);
						do {
							delay(1);
							if (isGone(NULL)) 
								terminate;
							if (GetPlayerInput(-1,INPUT_BUTTONS)&BT_JUMP) {
								SetActorVelocity(0,GetActorVelX(0),GetActorVelY(0),GetActorProperty(0,APROP_JumpZ),false,false);
								break;
							}
						}  while (!checkFooting(true,-2.0));
						TakeInventory("8C On actor gravity", 1);
						continue;
					}
				delay(1);
			}
		break;

		case C8_XY_DIST:
			if (!arg1) arg1 = GetActorProperty(0, APROP_TARGETTID);
			SetResultValue(xyDistance(0,arg1));
		break;

		case C8_Z_DIST:
			if (!arg1) arg1 = GetActorProperty(0, APROP_TARGETTID);
			SetResultValue(zDistance(0,arg1));
		break;

		case C8_IS_GONE:
			SetResultValue(isGone(arg1));
		break;
		
		case C8_COPY_VELOCITY:
			GiveInventory("8C follower",1);
			do {
				arg2 = ACS_NamedExecuteWithResult("8C", GET_VELOCITY, arg1);				
				SetActorVelocity(0, 
					((1-((arg2>>23)&0x2))*(arg2&0xFF))<<16, 
					((1-((arg2>>24)&0x2))*(arg2&0xFF00))<<8, 
					(1-((arg2>>25)&0x2))*(arg2&0xFF0000), 
				false, false);
				delay(1);
			} while (CheckInventory("8C follower"));
		break;

		case GET_VELOCITY:
			SetActivator(0, arg1);
			SetResultValue(
				(abs(GetActorVelX(0)>>16)&0xFF) |
				((abs(GetActorVelY(0)>>16)&0xFF) << 8) |
				((abs(GetActorVelZ(0)>>16)&0xFF) << 16) |
				((GetActorVelX(0)<0)<<24) |
				((GetActorVelY(0)<0)<<25) |
				((GetActorVelZ(0)<0)<<26)
			);
		break;

		// detect lateral collision with terrain/actors. Used i.e by Cutting Wheel to climb
		case C8_CHECK_COLLISION:
			arg1 = GetActorAngle(0)+ 0.25*arg1;
			int i = GetActorProperty(0,APROP_Radius)>>16;

			// increase this to help non axis aligned collision detection but at the cost of overall worsened collision accuracy
			arg3 = (i*5)/3;
			
			if (arg2) 	int tmp = "8C Check Collision";
			else 			tmp = "8C Check Terrain";

			arg2 = UniqueTID();
			while (true) {
				type = !Spawn(tmp,
					GetActorX(0) + (arg3 * cos(arg1)) - i*sin(arg1),
					GetActorY(0) + (arg3 * sin(arg1)) + i*cos(arg1),
					GetActorZ(0), arg2);
				if (type) break;
				type = !Spawn(tmp,
					GetActorX(0) + (arg3 * cos(arg1)) + i*sin(arg1),
					GetActorY(0) + (arg3 * sin(arg1)) - i*cos(arg1),
					GetActorZ(0)+GetActorProperty(0,APROP_Height)-12.0, arg2);
				break;
			}
			SetResultValue(type);
			Thing_Remove(arg2);
		break;

		// core_unsolidplayer but more simple, usable for monsters
		case C8_SOLID_TOGGLE:
			if (!arg1) {
				GiveInventory("PlayerPropertyUnsolid", 1);
				GiveInventory("PlayerPropertySolidOff",1);
			}
			else if (CheckInventory("PlayerPropertyUnsolid")) {
				TakeInventory("PlayerPropertyUnsolid", 1);				
				if (!CheckInventory("PlayerPropertyUnsolid"))
					GiveInventory("PlayerPropertySolidOn", 1);
			}
		break;

		// to generalize
		case C8_GET_USERVAR:
			if (arg1) SetActivator(0,arg1);
			SetResultValue(GetUserVariable(0, "user_colorId"));
		break;

		// 6b "map_teleportfog" script overwrite
		case NO_8C_FOG:
			if (Timer() < 2) delay(2); // give bootup time to determine isMap8C
			if (isMap8C) terminate;
			// edit end
			arg1 = ACS_NamedExecuteWithResult("core_getptrtid", AAPTR_TARGET);
			arg2 = UniqueTID();
			SpawnForced("TeleportFogFX", GetActorX(0), GetActorY(0), GetActorZ(0), arg2, 0);
			SetActivator(arg2);
			SetPointer(AAPTR_TARGET, arg1);
			SetActivator(arg1);
			Thing_SetTranslation(arg2, -1);
			Thing_ChangeTID(arg2, 0);
		break;

		// From control disable to control enabled again, takes 130 tics
		case INTRO_ANIMATION:
			SetActorProperty(0, APROP_Health, 1);
			ACS_NamedExecuteWithResult("core_freezeplayer", 0, true);
			ACS_NamedExecuteWithResult("core_invulnerableplayer", 0, true);
			ACS_NamedExecuteWithResult("core_invisibleplayer", 0, true);
			ACS_NamedExecuteWithResult("core_nogravityplayer", 0, true);
			GiveInventory("noInteractionOn",1);
			
			arg3 = CAMERA_TID+PlayerNumber();
			arg2 = ActivatorTID();
			SpawnForced("8C Camera",0,0,0,arg3,0);
			SetActivator(arg3);
			SetPointer(AAPTR_TARGET,arg2);
			SetActivator(arg2);
			
		 	ACS_NamedExecuteWithResult("8C CS", CS_INTRO_HUD_ANIMATION, PlayerNumber());
		 	delay(INCARNATION_PREINTRO_TIME);
			ACS_NamedExecuteWithResult("core_invisibleplayer", 0, false);
			ACS_NamedExecuteWithResult("core_nogravityplayer", 0, false);
			GiveInventory("noInteractionOff",1);
			SetActorState(0,"intro8C",true);
			
			// ---- INCARNATION_INTRO_SPAWN TIME start ---- //
			delay(35);
			type = ((GetActorProperty(0,APROP_SpawnHealth)-1)<<16)/40;
			for (i=1; i <= 40; i++) {
				SetActorProperty(0,APROP_Health, 1+((type*i)>>16));
				if (i%2) LocalAmbientSound("8CHPFILL", 127);
				delay(1);
			}
			SetActorProperty(0,APROP_Health, GetActorProperty(0,APROP_SpawnHealth));
			delay(15);
			ACS_NamedExecuteWithResult("core_freezeplayer", 0, false);
			ACS_NamedExecuteWithResult("core_invulnerableplayer", 0, false);
			// ---- INCARNATION_INTRO_SPAWN TIME end ---- //
		break;

		case C8_GOAL_CHECK:
			if ((GetActorProperty(0, APROP_Accuracy)) < act) {
				SetResultValue(true);
				terminate;
			}
			SetResultValue(false);
			arg2 = GetActorProperty(0, APROP_Mass);
			for (i = 0; i < MAX_CLIENTS; i++)
				if (PlayerInGame(i) && isSelectedPlayer(i))
					if ((xyDistance(0,i+1000) < arg2) && !CheckActorInventory(i+1000,"8C Goal Status"))
						GiveActorInventory(i+1000, "8C Enter Goal", 1);
		break;

		/*
		prevents dealing or receiving damage/effects when enabled
		@arg1 on/off
		*/
		case C8_PACIFIST:
			if (arg1) {
				if (!CheckInventory("8C Pacifist Stack"))
					GiveInventory("8C Pacifist Giver",1);
				GiveInventory("8C Pacifist Stack", 1);
			}
			else {
				TakeInventory("8C Pacifist Stack", 1);
				if (!CheckInventory("8C Pacifist Stack"))
					TakeInventory("8C Pacifist",1);
			}
		break;

		case C8_ACT_COMPLETE:
			act++;
			AmbientSound("8CGOAL", 127);
			for (i = 0; i < MAX_CLIENTS; i++) {
				if (PlayerInGame(i)) {
					SetActivatorToPlayer(i);
					if (CheckInventory("8C Goal Status")) {
						ACS_NamedExecuteWithResult("core_freezeplayer", 0, false);
						ACS_NamedExecuteWithResult("8C", C8_PACIFIST, false);
						clearInv("8C Goal Status");
					}}}
			setServerPacify(true, true, true);
			ACS_NamedExecuteWithResult("8C CS", CS_COMPLETE_MESSAGE, false);
			delay(105);
			setmusic("");
			ACS_NamedExecuteWithResult("8C CS", CS_CLOSURE_ANIMATION, 60);
			delay(60);	
			setServerPacify(false, true, false);
			actStart();
		break;

	}
}

function void setServerPacify(bool value, bool alterFreeze, bool freezeValue) {
	for (int i = 0; i < MAX_CLIENTS; i++) {
		if (PlayerInGame(i)) {
			SetActivatorToPlayer(i);
				ACS_NamedExecuteWithResult("8C", C8_PACIFIST, value);
				if (alterFreeze)
					ACS_NamedExecuteWithResult("core_freezeplayer", 0, freezeValue);
				
		}
	}
}

script "goal" (void) {
	spawnGoal(550,128,1);
}

script "8C API" (int type, int arg1, int arg2, int arg3) {
	switch(type) {
		/*
		@arg1 map spot TID
		@arg2 radius
		@arg3 goal of arg3 act (1/2)
		*/
		case C8_API_SPAWN_GOAL:
			type = UniqueTID();
			SpawnSpotFacingForced("8C Goal", arg1, type);
			SetActorProperty(type, APROP_Mass, arg2);
			SetActorProperty(type, APROP_Accuracy, arg3);
			Thing_ChangeTID(type,0);
		break;
	}
}



// <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// 			W             I              N           N             A              B             L          E.
// >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
function bool isGameWinnable(int count) {
	return (count >= ((selectedPlayersSize/2)+1));
}



function void actOverCheck(void) {
	if (!isGameWinnable(selectedPlayerCount(false))) {
		log(s:"game lost...");
		// lost sequence
	}
}

function int selectedPlayerCount(bool ignoreStatus) {
	int count = 0;
	for (int i = 0; i < MAX_CLIENTS; i++) {
		if (ignoreStatus) 
			count += isSelectedPlayer(i);
		else
			if (PlayerInGame(i) && isSelectedPlayer(i)) {
				SetActivatorToPlayer(i);
				if (!isGone(NULL)) count++;
			}
	}
	log(s:"\cg\n>>> selected players ", d:count);
	return count;
}

function bool deflectAttack(void) {
	if (CheckInventory("8C Deflect")) {
		PlaySound(0,"8CPAIN02",CHAN_VOICE,1.0,false);
		int isValid = SetActivator(0, AAPTR_DAMAGE_INFLICTOR);
		SpawnForced("8C Deflect gfx", GetActorX(0), GetActorY(0), GetActorZ(0));
		if (isValid && !CheckFlag(0, "DONTREFLECT")) {
            SetPointer(AAPTR_TARGET, 0, AAPTR_DAMAGE_TARGET);
			SetActorVelocity(0, -GetActorVelY(0), GetActorVelX(0), -16.0, false, false);
		}
		return true;
	}
    return false;
}

function bool isPacifist(void) {
	SetActivator(0, AAPTR_DAMAGE_TARGET); if (CheckInventory("8C Pacifist")) return true;
	SetActivator(0, AAPTR_DAMAGE_SOURCE); if (CheckInventory("8C Pacifist")) return true;
	return false;
}









function void createHero(int player, int slot, int heroIndex) {
    setPlayerHero(player, slot, heroIndex);
    clearWeps(player, slot);
	setExp(player, slot, 0);
    if (slot >= 0)
        dbOperation(player, DB_CREATE_HERO, slot, heroIndex);
}

function void deleteHero(int player, int slot) {
    setPlayerHero(player, slot, -1);
    if (slot >= 0)
        dbOperation(player, DB_DELETE_HERO, slot, 0);
}










